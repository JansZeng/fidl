# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../mojo_sdk.gni")
import("rules.gni")

group("python") {
  deps = [
    ":bindings",
  ]
}

action("import_interface_bindings") {
  visibility = [ ":bindings" ]
  script = rebase_path("mojo/public/tools/gn/unzip.py", ".", mojo_root)
  timestamp = "$target_gen_dir/${target_name}.outputstamp"
  mojom_deps = [ "../interfaces/bindings:bindings_python" ]
  inputs = []
  foreach(d, mojom_deps) {
    dep_name = get_label_info(d, "name")
    dep_target_out_dir = get_label_info(d, "target_out_dir")
    inputs += [ "$dep_target_out_dir/$dep_name.pyzip" ]
  }
  outputs = [
    timestamp,
  ]
  rebase_inputs = rebase_path(inputs, root_build_dir)
  rabase_output = rebase_path("$root_out_dir/python", root_build_dir)
  rebase_timestamp = rebase_path(timestamp, root_build_dir)
  args = [
    "--inputs=${rebase_inputs}",
    "--output=${rabase_output}",
    "--timestamp=${rebase_timestamp}",
  ]

  deps = mojom_deps
}

group("bindings") {
  deps = [
    ":import_interface_bindings",
    ":serialization_bindings",
  ]
}

# This target lists dependencies needed to use generated mojom files for
# serialization/deserialization only, but not the full mojo application stack.
copy("serialization_bindings") {
  sources = [
    "mojo_bindings/__init__.py",
    "mojo_bindings/descriptor.py",
    "mojo_bindings/interface_reflection.py",
    "mojo_bindings/messaging.py",
    "mojo_bindings/promise.py",
    "mojo_bindings/reflection.py",
    "mojo_bindings/serialization.py",
  ]
  outputs = [
    "$root_out_dir/python/mojo_bindings/{{source_file_part}}",
  ]
}

python_package("packaged_bindings") {
  sources = [
    "mojo_bindings/__init__.py",
    "mojo_bindings/descriptor.py",
    "mojo_bindings/interface_reflection.py",
    "mojo_bindings/messaging.py",
    "mojo_bindings/promise.py",
    "mojo_bindings/reflection.py",
    "mojo_bindings/serialization.py",
  ]

  deps = [
    "../interfaces/bindings:bindings_python",
  ]
}
